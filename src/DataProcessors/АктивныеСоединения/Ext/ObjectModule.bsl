Перем мИмяСобытияВЖурналеРегистрации Экспорт;

//
//
Процедура ЗаполнитьТаблицуАктивныхСоединений()  Экспорт
	
	ЭтотОбъект.АктивныеСоединения.Очистить();
	
	Для Каждого ТекущаяСтрокаТаблицаОбработки Из ЭтотОбъект.ТаблицаОбработки Цикл
				
		ЭтотОбъект.АктивныеСоединения.Загрузить(ОбщегоНазначенияСервер.ОбъединитьИдентичныеТаблицы(ЭтотОбъект.АктивныеСоединения.Выгрузить(), ПолучитьТаблицуАктивныхСоединений(ТекущаяСтрокаТаблицаОбработки.ИнформационнаяБаза)));
		
	КонецЦикла;
	
КонецПроцедуры  

//
//
Функция ПолучитьТаблицуАктивныхСоединений(ИнформационнаяБаза, ИсключатьТекущееСистемноеCOMСоединение = Истина)  Экспорт
	
	лТаблицаАктивныхСоединений = ЭтотОбъект.АктивныеСоединения.Выгрузить();
	лТаблицаАктивныхСоединений.Очистить();
	
	МассивАктивныхСоединений = ЗадачиНаСервере.ПолучитьМассивАктивныхСоединений(ЗадачиНаСервере.ПолучитьСтруктуруПараметровОбработки(ИнформационнаяБаза));

	Если МассивАктивныхСоединений <> Неопределено Тогда
		
		ИнформационнаяБазаПользователь = ИнформационнаяБаза.Пользователь;
		
		Для Каждого ТекущееЗначениеМассивАктивныхСоединений Из МассивАктивныхСоединений Цикл
			
			ТекущееЗначениеМассивАктивныхСоединенийПользовательИмя = ТекущееЗначениеМассивАктивныхСоединений.Пользователь.Имя;
			ТекущееЗначениеМассивАктивныхСоединенийИмяПриложения = ТекущееЗначениеМассивАктивныхСоединений.ИмяПриложения;
			
			Если ИсключатьТекущееСистемноеCOMСоединение И НРег(ТекущееЗначениеМассивАктивныхСоединений.ИмяПриложения) = "comconnection" И 
				ТекущееЗначениеМассивАктивныхСоединенийПользовательИмя = СокрЛП(ИнформационнаяБазаПользователь) Тогда 
				Продолжить;
			КонецЕсли;
			
			НоваяСтрокаТаблицаАктивныхСоединений = лТаблицаАктивныхСоединений.Добавить();
			//ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицаАктивныхСоединений, ТекущееЗначениеМассивАктивныхСоединений);
			НоваяСтрокаТаблицаАктивныхСоединений.ИнформационнаяБаза = ИнформационнаяБаза;
			НоваяСтрокаТаблицаАктивныхСоединений.Пользователь = ТекущееЗначениеМассивАктивныхСоединенийПользовательИмя;
			НоваяСтрокаТаблицаАктивныхСоединений.ИмяПриложения = ПолучитьПредставлеинеИмениПриложения(ТекущееЗначениеМассивАктивныхСоединенийИмяПриложения);
			НоваяСтрокаТаблицаАктивныхСоединений.ИмяКомпьютера = ТекущееЗначениеМассивАктивныхСоединений.ИмяКомпьютера;
			НоваяСтрокаТаблицаАктивныхСоединений.НачалоСоединения = ТекущееЗначениеМассивАктивныхСоединений.НачалоСоединения;
			НоваяСтрокаТаблицаАктивныхСоединений.НомерСеанса = ТекущееЗначениеМассивАктивныхСоединений.НомерСеанса;
			НоваяСтрокаТаблицаАктивныхСоединений.НомерСоединения = ТекущееЗначениеМассивАктивныхСоединений.НомерСоединения;
			
		КонецЦикла;
	КонецЕсли;
	
	лТаблицаАктивныхСоединений.Сортировать("ИнформационнаяБаза, Пользователь");
	
	Возврат лТаблицаАктивныхСоединений;
КонецФункции

//
//
Функция ПолучитьПредставлеинеИмениПриложения(ИмяПриложения) Экспорт 
	Результат = ИмяПриложения;
	
	Если ИмяПриложения = "1CV8" Тогда
		Результат = "Толстый клиент";
	ИначеЕсли ИмяПриложения = "1CV8C" Тогда
		Результат = "Тонкий клиент";
	ИначеЕсли ИмяПриложения = "WebClient" Тогда
		Результат = "Веб-клиент";
	ИначеЕсли ИмяПриложения = "Designer" Тогда
		Результат = "Конфигуратор";
	ИначеЕсли ИмяПриложения = "WSConnection" Тогда
		Результат = "Web-Сервис";
	ИначеЕсли ИмяПриложения = "SrvrConsole" Тогда
		Результат = "Консоль кластера";
	ИначеЕсли ИмяПриложения = "COMConsole" Тогда
		Результат = "Административная сессия внешнего соединения";
	ИначеЕсли ИмяПриложения = "RAS" Тогда
		Результат = "Сервера администрирования";
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

мИмяСобытияВЖурналеРегистрации = "GetActiveConnections";