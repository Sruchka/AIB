
&НаСервере
//
//
Функция СохранитьДвоичныеДанныеФайлаВыгрузкиВКаталог(РелизОбновления, КаталогСохранения) Экспорт
	Результат = Ложь;	
	ДанныеЗаписи = ПолучитьПолныеДанныеЗаписи(РелизОбновления);
	Если ДанныеЗаписи <> Неопределено Тогда
		Если ДанныеЗаписи.Количество() > 0 Тогда 
			ДвоичныеДанныеФайлаАрхива = ДанныеЗаписи.ДвоичныеДанныеФайлаАрхива.Получить();			
			Попытка
				
				КаталогСохранения = КаталогСохранения + ?(Прав(КаталогСохранения, 1) = "\", "", "\");
				
				ИмяВременногоФайлаАрхива = КаталогВременныхФайлов() + "_" + ЗадачиНаСервере.ЗаменитьСпецсимволыНаПодчеркивание(СокрЛП(РелизОбновления)) + ".zip";				
				ДвоичныеДанныеФайлаАрхива.Записать(ИмяВременногоФайлаАрхива);
								
				ЧтениеZIP = Новый ЧтениеZipФайла();
				ЧтениеZIP.Открыть(ИмяВременногоФайлаАрхива);
				ЧтениеZIP.ИзвлечьВсе(КаталогСохранения, РежимВосстановленияПутейФайловZIP.Восстанавливать);				
				ЧтениеZIP.Закрыть();
				
				УдалитьФайлы(ИмяВременногоФайлаАрхива);		
				
				Результат = Истина;
			Исключение
				ЗаписьЖурналаРегистрации("СохранениеАрхиваРелизаВКаталог", УровеньЖурналаРегистрации.Ошибка,,, "Ошибка: " + ОписаниеОшибки());				
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	Возврат Результат;
КонецФункции

&НаСервере
//
//
Процедура НайтиФайлыПоПодпапкам(НайденныеФайлы, Каталог, Маска)
    Если НайденныеФайлы = Неопределено Тогда
        НайденныеФайлы = Новый Массив;
    КонецЕсли;
    Путь = Каталог;
    МассивФайлов = НайтиФайлы(Путь, Маска);
    Для Каждого НайденныйФайл Из МассивФайлов Цикл
        НайденныеФайлы.Добавить(НайденныйФайл);    
    КонецЦикла;
    МассивКаталогов = НайтиФайлы(Путь, "*");
    Для Каждого Папка Из МассивКаталогов Цикл
        Попытка
            Если Папка.ЭтоКаталог() Тогда
                НайтиФайлыПоПодпапкам(НайденныеФайлы, Папка.ПолноеИмя, Маска);        
            КонецЕсли;
        Исключение
            Сообщить(ОписаниеОшибки());
        КонецПопытки;
    КонецЦикла;
КонецПроцедуры

&НаСервере
//
//
Функция ЗагрузитьДвоичныеДанныеАрхиваИзКаталога(РелизОбновления, КаталогЗагрузки) Экспорт
	Результат = Ложь;	
	ДанныеЗаписи = РегистрыСведений.ХранилищеРелизовОбновлений.СоздатьМенеджерЗаписи();
	ДанныеЗаписи.РелизОбновления = РелизОбновления;	
	ДанныеЗаписи.Прочитать();
	Попытка
		Если ДанныеЗаписи.Выбран() Тогда
			
			КаталогЗагрузки = КаталогЗагрузки + ?(Прав(КаталогЗагрузки, 1) = "\", "", "\");
			
			ИмяВременногоФайлаАрхива = КаталогВременныхФайлов() + "_" + ЗадачиНаСервере.ЗаменитьСпецсимволыНаПодчеркивание(СокрЛП(РелизОбновления)) + ".zip";
			
			ЗаписьZIP = Новый ЗаписьZipФайла();
			ЗаписьZIP.Открыть(ИмяВременногоФайлаАрхива,, "Архив релиза обновления <" + СокрЛП(РелизОбновления) + "> из хранилища", МетодСжатияZIP.Сжатие, УровеньСжатияZIP.Максимальный, МетодШифрованияZIP.Zip20);			
			ЗаписьZIP.Добавить(КаталогЗагрузки + "*", РежимСохраненияПутейZIP.СохранятьОтносительныеПути,РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);			
			ЗаписьZIP.Записать();
			ЗаписьZIP = Неопределено;			
			
			ДанныеЗаписи.ДвоичныеДанныеФайлаАрхива = Новый ХранилищеЗначения(Новый ДвоичныеДанные(ИмяВременногоФайлаАрхива), Новый СжатиеДанных(9));
			ДанныеЗаписи.АрхивЗагруженВХранилище = Истина;
						
			УдалитьФайлы(ИмяВременногоФайлаАрхива);		
			
			ДанныеЗаписи.Записать(Истина);
			Результат = Истина;
		КонецЕсли;				
	Исключение
		ЗаписьЖурналаРегистрации("ЗагрузкаАрхиваРелиза", УровеньЖурналаРегистрации.Ошибка,,, "Ошибка: " + ОписаниеОшибки());				
	КонецПопытки;
	Возврат Результат;
КонецФункции

&НаСервере
//
//
Функция УдалитьДвоичныеДанныеФайлаАрхива(РелизОбновления) Экспорт
	Результат = Ложь;	
	ДанныеЗаписи = РегистрыСведений.ХранилищеРелизовОбновлений.СоздатьМенеджерЗаписи();
	ДанныеЗаписи.РелизОбновления = РелизОбновления;	
	ДанныеЗаписи.Прочитать();
	Попытка
		Если ДанныеЗаписи.Выбран() Тогда
			ДанныеЗаписи.ДвоичныеДанныеФайлаАрхива = Новый ХранилищеЗначения(Неопределено);
			ДанныеЗаписи.АрхивЗагруженВХранилище = Ложь;
		КонецЕсли;
		ДанныеЗаписи.Записать(Истина);
		Результат = Истина;
	Исключение
		ЗаписьЖурналаРегистрации("УдалениеАрхиваРелиза", УровеньЖурналаРегистрации.Ошибка,,, "Ошибка: " + ОписаниеОшибки());
	КонецПопытки;
	Возврат Результат;
КонецФункции

&НаСервере
//
//
Функция ПолучитьПолныеДанныеЗаписи(РелизОбновления) Экспорт
	ДанныеЗаписи = Неопределено;	
	Отбор = Новый Структура;	
	Отбор.Вставить("РелизОбновления", РелизОбновления);		
	ДанныеЗаписи = РегистрыСведений.ХранилищеРелизовОбновлений.Получить(Отбор);
	ДанныеЗаписи.Вставить("РелизОбновления", РелизОбновления);	
	Возврат ДанныеЗаписи;
КонецФункции

&НаСервере
//
//
Функция СформироватьИмяВременногоКаталогаАрхива(РелизОбновления) Экспорт
	Возврат КаталогВременныхФайлов() + ЗадачиНаСервере.ЗаменитьСпецсимволыНаПодчеркивание(СокрЛП(РелизОбновления));
КонецФункции
