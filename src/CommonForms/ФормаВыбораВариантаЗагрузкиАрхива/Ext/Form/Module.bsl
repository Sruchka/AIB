
&НаКлиенте
//
//
Процедура ПутьКАрхивуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)	
	СтандартнаяОбработка = Ложь;	
	ДиалогВыбораКаталога = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораКаталога.ПолноеИмяФайла = "";
	Если ЗначениеЗаполнено(ПутьКАрхиву) Тогда
		КаталогРасположения = ПутьКАрхиву;
		ДиалогВыбораКаталога.ПолноеИмяФайла = КаталогРасположения;
		ПолноеИмяФайлаАрхива = ЗадачиНаКлиенте.ПолучитьИмяФайлаАрхиваИзПолногоПути(КаталогРасположения);
		КаталогРасположения = СтрЗаменить(КаталогРасположения, ПолноеИмяФайлаАрхива, "");
	Иначе
		КаталогРасположения = ЗадачиНаСервере.ПолучитьОсновнойКаталогАрхива();
	КонецЕсли;	
	ДиалогВыбораКаталога.МножественныйВыбор = Ложь;	
	ДиалогВыбораКаталога.Заголовок = "Выберите файл архива";
	Фильтр = "(*.dt)|*.dt";
	ДиалогВыбораКаталога.Фильтр = Фильтр;
	ДиалогВыбораКаталога.Каталог = КаталогРасположения;
	Если ДиалогВыбораКаталога.Выбрать() Тогда
		ПутьКАрхиву = ДиалогВыбораКаталога.ПолноеИмяФайла;
		ЭтаФорма.Модифицированность = Истина;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
//
//
Процедура ЗагружаемВнешнийФайлПриИзменении(Элемент)
	УправлениеЭлементамиФормы();
КонецПроцедуры

&НаКлиенте
//
//
Процедура УправлениеЭлементамиФормы()	
	Элементы.ПутьКАрхиву.Доступность = ЗагружаемВнешнийФайл;
	Элементы.ВыбратьВнешнийФайл.Доступность = ЗагружаемВнешнийФайл;
	Элементы.ГруппаХранилищеАрхивов.Доступность = НЕ ЗагружаемВнешнийФайл;
КонецПроцедуры

&НаКлиенте
//
//
Процедура Отмена(Команда)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
//
//
Процедура ПриОткрытии(Отказ)
	ЗагружатьИзАрхиваВХранилище = Истина;
	УправлениеЭлементамиФормы();
КонецПроцедуры

&НаКлиенте
//
//
Процедура ВыбратьВнешнийФайл(Команда)
	Если НЕ ЗначениеЗаполнено(ПутьКАрхиву) Тогда
		ОбщегоНазначенияСервер.ВывестиСообщение("Не задан файл архива!");
		Возврат;
	КонецЕсли;
	ОбработкаВыбора(Новый Структура("ПутьКАрхиву, АрхивЗагруженВХранилище, ТипАрхива", ПутьКАрхиву, Ложь, ПредопределенноеЗначение("Перечисление.ТипыФайловАрхива.Архив1С")));
КонецПроцедуры

&НаКлиенте
//
//
Процедура ОбработкаВыбора(ДанныеАрхива)	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("ЗагружаемВнешнийФайл", НЕ ДанныеАрхива.АрхивЗагруженВХранилище);
	ПараметрыВыбора.Вставить("ИмяАрхива", ДанныеАрхива.ПутьКАрхиву);
	ПараметрыВыбора.Вставить("ТипАрхива", ДанныеАрхива.ТипАрхива);
	ЭтаФорма.Закрыть(ПараметрыВыбора);
КонецПроцедуры

&НаКлиенте
//
//
Процедура ХранилищеАрхивовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОбработкаВыбора(ХранилищеАрхивовВыборЗначенияСервер(ВыбраннаяСтрока));
КонецПроцедуры

&НаСервере
//
//
Функция ХранилищеАрхивовВыборЗначенияСервер(ВыбраннаяСтрока)
	ДанныеАрхива = РегистрыСведений.ХранилищеАрхивов.ПолучитьПолныеДанныеЗаписи(ВыбраннаяСтрока.ДатаСобытия, ВыбраннаяСтрока.ИнформационнаяБаза, ВыбраннаяСтрока.ТипАрхива);
	ПутьФайлаАрхива = ДанныеАрхива.ПутьКАрхиву;
	Если ЗагружатьИзАрхиваВХранилище И ДанныеАрхива.АрхивЗагруженВХранилище Тогда
		ПутьФайлаАрхива = РегистрыСведений.ХранилищеАрхивов.СформироватьИмяВременногоФайлаАрхива(ДанныеАрхива.ИнформационнаяБаза);
		РегистрыСведений.ХранилищеАрхивов.СохранитьДвоичныеДанныеФайлаВыгрузкиВКаталог(ДанныеАрхива.ИнформационнаяБаза, ДанныеАрхива.ДатаСобытия, ДанныеАрхива.ТипАрхива, ПутьФайлаАрхива);
	КонецЕсли;
	Возврат Новый Структура("ПутьКАрхиву, АрхивЗагруженВХранилище, ТипАрхива", ПутьФайлаАрхива, ДанныеАрхива.АрхивЗагруженВХранилище, ВыбраннаяСтрока.ТипАрхива);
КонецФункции

&НаСервере
//
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЭтаФорма.Параметры <> Неопределено Тогда
		НовыйЭлементОтбора = ЭтаФорма.ХранилищеАрхивов.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		НовыйЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		НовыйЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ИнформационнаяБаза");
		НовыйЭлементОтбора.ПравоеЗначение = ЭтаФорма.Параметры.ПараметрыОбработки.ИнформационнаяБаза;	
	КонецЕсли;
КонецПроцедуры



