
//
//
Функция ТэгЖурналаРегистрации() Экспорт
	 Возврат "SheduledTask";
КонецФункции

//
//
Процедура ОбновитьРегламентноеЗадание(СсылкаНаЭлемент) Экспорт
	
	Ключ = СокрЛП(СсылкаНаЭлемент.Ссылка.УникальныйИдентификатор());
	
	ТекущееРегламентноеЗадание = Неопределено;
	
	МассивЗаданий = РегламентныеЗадания.ПолучитьРегламентныеЗадания(Новый Структура("Ключ", Ключ));
	Если МассивЗаданий.Количество() = 0 Тогда
		
		ТекущееРегламентноеЗадание = РегламентныеЗадания.СоздатьРегламентноеЗадание(Метаданные.РегламентныеЗадания.РегламентнаяОбработкаДанных);		
		ТекущееРегламентноеЗадание.Ключ = Ключ;		
		
		Параметры = Новый Массив();
		Параметры.Добавить(Ключ);
		ТекущееРегламентноеЗадание.Параметры = Параметры;
		
	Иначе
		
		ТекущееРегламентноеЗадание = МассивЗаданий[0];
		
	КонецЕсли;	
	
	ПерезаписатьРегламентноеЗадание = (ТекущееРегламентноеЗадание.Ключ <> Ключ
										ИЛИ ТекущееРегламентноеЗадание.Наименование <> СсылкаНаЭлемент.Наименование
										ИЛИ ТекущееРегламентноеЗадание.Использование <> СсылкаНаЭлемент.Включено);
									
	Если ПерезаписатьРегламентноеЗадание Тогда 
		ТекущееРегламентноеЗадание.Наименование = СсылкаНаЭлемент.Наименование;
		ТекущееРегламентноеЗадание.Использование = СсылкаНаЭлемент.Включено;
		ТекущееРегламентноеЗадание.Записать();
	КонецЕсли;
	
КонецПроцедуры

//
//
Функция ОбновитьРасписаниеРегламентногоЗадния(СсылкаНаЭлемент, Расписание = Неопределено) Экспорт
	
	Результат = Неопределено;
	
	Ключ = СсылкаНаЭлемент.Ссылка.УникальныйИдентификатор();
	
	ТекущееРегламентноеЗадание = Неопределено;
	
	МассвЗаданий = РегламентныеЗадания.ПолучитьРегламентныеЗадания(Новый Структура("Ключ", Ключ));
	Если МассвЗаданий.Количество() > 0 Тогда
		
		ТекущееРегламентноеЗадание = МассвЗаданий[0];		
		
	КонецЕсли;
	
	Если ТекущееРегламентноеЗадание <> Неопределено Тогда
		
		Если Расписание <> Неопределено Тогда
			ТекущееРегламентноеЗадание.Расписание = Расписание;
			ТекущееРегламентноеЗадание.Записать();
		КонецЕсли;
		
		Результат = ТекущееРегламентноеЗадание.Расписание;
		
	КонецЕсли;
	
	Возврат Результат;
КонецФункции