
&НаКлиенте
//
//
Процедура СформироватьТекстСпискаБаз(Команда)
	ТекущаяСсылка = Элементы.Список.ТекущаяСтрока;
	Если НЕ ЗначениеЗаполнено(ТекущаяСсылка) Тогда
		ОбщегоНазначенияСервер.ВывестиСообщение("Не выбран элемент справчника!");
		Возврат;
	КонецЕсли;	
	
	//ОткрытьФорму("ОбщаяФорма.ФормаПредпросмотраТекстовойИнформации", Новый Структура("ТекстРезультат", WebСервисWebCommonInfoBases.СформироватьТекстСпискаБаз(ТекущаяСсылка)), ЭтаФорма);
	ЗадачиНаКлиенте.ПоказатьТекстовуюИнформацию("Текст списка баз", WebСервисWebCommonInfoBases.СформироватьТекстСпискаБаз(ТекущаяСсылка));
	
	
КонецПроцедуры

&НаСервере
//
//
Процедура ОтключитьДоступностьСпискаНаСервере(ПараметрыОповещения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДоступКСпискамИнформационныхБазДоступныеСпискиОбщихИБ.Ссылка
		|ИЗ
		|	Справочник.ДоступКСпискамИнформационныхБаз.ДоступныеСпискиОбщихИБ КАК ДоступКСпискамИнформационныхБазДоступныеСпискиОбщихИБ
		|ГДЕ
		|	ДоступКСпискамИнформационныхБазДоступныеСпискиОбщихИБ.СписокОбщихИБ = &СписокОбщихИБ";	
	Запрос.УстановитьПараметр("СписокОбщихИБ", ПараметрыОповещения.ТекущаяСсылка);	
	РезультатЗапроса = Запрос.Выполнить();	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ТекущийОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
		ТекущийОбъектДоступныеСпискиОбщихИБ = ТекущийОбъект.ДоступныеСпискиОбщихИБ.Выгрузить();
		
		МассивСтрок = ТекущийОбъектДоступныеСпискиОбщихИБ.НайтиСтроки(Новый Структура("СписокОбщихИБ", ПараметрыОповещения.ТекущаяСсылка));
		Для Каждого ТекущийЭлементМассивСтрок Из МассивСтрок Цикл
			ТекущийОбъектДоступныеСпискиОбщихИБ.Удалить(ТекущийЭлементМассивСтрок);
		КонецЦикла;
		
		ТекущийОбъект.ДоступныеСпискиОбщихИБ.Загрузить(ТекущийОбъектДоступныеСпискиОбщихИБ);
		
		ТекущийОбъект.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
//
//
Процедура ОтключитьДоступностьСписка(Команда)
	
	ТекущаяСсылка = Элементы.Список.ТекущаяСтрока;
	Если НЕ ЗначениеЗаполнено(ТекущаяСсылка) Тогда
		ОбщегоНазначенияСервер.ВывестиСообщение("Не выбран элемент справчника!");
		Возврат;
	КонецЕсли;	
	
	ПараметрыОповещения = Новый Структура("ТекущаяСсылка", ТекущаяСсылка);
	ПоказатьВопрос(Новый ОписаниеОповещения("ОтключитьДоступностьСпискаОписаниеОповещения", ЭтаФорма, ПараметрыОповещения), "Вы уверены, что хотите удлить ссылку на текущий список из всех доступов?", РежимДиалогаВопрос.ДаНет,,, "Отключить доступность списка баз");	
	
КонецПроцедуры

&НаКлиенте
//
//
Процедура ОтключитьДоступностьСпискаОписаниеОповещения(РезультатОповещения, ПараметрыОповещения) Экспорт
	
	// Запрос на очистку содержимого списка
	Если РезультатОповещения = КодВозвратаДиалога.Да Тогда
		ОтключитьДоступностьСпискаНаСервере(ПараметрыОповещения);
	КонецЕсли;
	
КонецПроцедуры
